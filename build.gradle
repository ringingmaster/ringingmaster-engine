apply plugin: 'java'

group 'org.ringingmaster'
version '1.0.0'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
}

sourceSets {
    testLongRunning {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

configurations {
    testLongRunningCompile.extendsFrom testCompile
    testLongRunningRuntime.extendsFrom testRuntime
}

dependencies {
    compile 'org.ringingmaster:ringingmaster-util:1.0.0'
    compile 'org.ringingmaster:ringingmaster-persist:1.0.0'

    // General
    compile 'net.jcip:jcip-annotations:1.0'

    // TEST: General
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8.47'

    // TEST: Logging
    testRuntime 'org.apache.logging.log4j:log4j-core:2.8.2'
    testRuntime 'org.apache.logging.log4j:log4j-slf4j-impl:2.8.2'
}

task testLongRunning(type: Test, dependsOn: jar) {
    group 'Verification'
    description 'Runs the longRunning tests.'
    testClassesDirs =  sourceSets.testLongRunning.output
    classpath = sourceSets.testLongRunning.runtimeClasspath
    systemProperties['jar.path'] = jar.archivePath
}

check.dependsOn testLongRunning