apply plugin: 'java'

group 'org.ringingmaster'
version '1.0.0'

compileJava {
    sourceCompatibility = 12
    targetCompatibility = 12
}

repositories {
    mavenCentral()
}

sourceSets {
    testLongRunning {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

configurations {
    testLongRunningCompile.extendsFrom testCompile
    testLongRunningRuntime.extendsFrom testRuntime
}

dependencies {
    implementation 'org.ringingmaster:ringingmaster-util:1.0.0'
    implementation 'org.ringingmaster:ringingmaster-persist:1.0.0'

    // General
    implementation 'com.google.guava:guava:27.1-jre'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
    implementation 'org.pcollections:pcollections:2.1.2'

    // Logging
    implementation 'org.slf4j:slf4j-api:1.7.25'

    // TEST: General
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.25.0'

    // TEST: Logging
    testRuntimeOnly 'org.apache.logging.log4j:log4j-core:2.8.2'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.8.2'
}

task testLongRunning(type: Test, dependsOn: jar) {
    group 'Verification'
    description 'Runs the longRunning tests.'
    testClassesDirs =  sourceSets.testLongRunning.output
    classpath = sourceSets.testLongRunning.runtimeClasspath
    systemProperties['jar.path'] = jar.archivePath
}

check.dependsOn testLongRunning