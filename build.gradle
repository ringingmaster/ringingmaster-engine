apply plugin: 'java'

group 'com.concurrentperformance.ringingmaster'
version '1.0.0'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
}

sourceSets {
    longRunningTest {
        java.srcDir file('src/long-running-test/java')
        resources.srcDir file('src/long-running-test/resources')
    }
}

dependencies {
    compile 'com.concurrentperformance.ringingmaster:ringingmaster-util:1.0.0'
    compile 'com.concurrentperformance.ringingmaster:ringingmaster-persist:1.0.0'

    longRunningTestCompile sourceSets.main.output
    longRunningTestCompile configurations.testCompile
    longRunningTestCompile sourceSets.test.output
    longRunningTestRuntime configurations.testRuntime

    // General
    compile 'com.google.guava:guava:19.0'
    compile 'net.jcip:jcip-annotations:1.0'

    // Logging
    compile 'org.slf4j:slf4j-api:1.7.21'

    // TEST: General
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'

    // TEST: Logging
    testRuntime 'org.apache.logging.log4j:log4j-core:2.5'
    testRuntime 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
}

task longRunningTest(type: Test, dependsOn: jar) {
    group 'Verification'
    description 'Runs the longRunning tests.'
    testClassesDirs =  sourceSets.longRunningTest.output
    classpath = sourceSets.longRunningTest.runtimeClasspath
    systemProperties['jar.path'] = jar.archivePath
}

check.dependsOn longRunningTest